# Some common test configurations to easily get up and running with Tilt for Tests
# (see docs: https://docs.tilt.dev/tests_in_tilt.html)


def _test_js(name, pkmanager, dir, deps=None, only_changed=True, with_install=False, project_root='', ignore=None, extra_args=None, **kwargs):
    if not project_root:
        project_root = dir

    install_deps = deps_for_package_manager(pkmanager, project_root)
    resource_deps = []
    if with_install:
        install_resource = pkmanager + '-install'
        install_cmd = 'cd {dir} && {pkmanager} install'.format(
            dir=dir, pkmanager=pkmanager)
        local_resource(install_resource, install_cmd, install_deps)
        resource_deps = [install_resource]

    extra_args_str = ''
    if extra_args:
        extra_args_str = ' '.join(extra_args)

    # https://jestjs.io/docs/cli#--onlychanged
    only_changed_flag = '-o'  # by default, run tests for files changed since last commit
    if not only_changed:
        only_changed_flag = ''

    cmd = 'cd {dir} && {pkmanager} test {only_changed_flag} --watchAll=false {extra_args_str}'.format(
        dir=dir, pkmanager=pkmanager, only_changed_flag=only_changed_flag, extra_args_str=extra_args_str)

    file_deps = deps
    if not file_deps:
        file_deps = dir

    all_ignores = []
    if ignore:
        all_ignores = [ig for ig in ignore]
    all_ignores.extend(install_deps + extra_ignores(pkmanager, project_root))
    local_resource(name, cmd, deps=file_deps, ignore=all_ignores, resource_deps=resource_deps, allow_parallel=True, **kwargs)


def test_jest_npm(name, dir, deps=None, only_changed=True, with_install=False, project_root='', ignore=None, extra_args=None, **kwargs):
    _test_js(name, 'npm', dir,
             deps=deps,
             only_changed=only_changed,
             with_install=with_install,
             project_root=project_root,
             ignore=ignore,
             extra_args=extra_args,
             **kwargs)


def test_jest_yarn(name, dir, deps=None, only_changed=True, with_install=False, project_root='', ignore=None, extra_args=None, **kwargs):
    _test_js(name, 'yarn', dir,
             deps=deps,
             only_changed=only_changed,
             with_install=with_install,
             project_root=project_root,
             ignore=ignore,
             extra_args=extra_args,
             **kwargs)


def verify_dep(f):
    if not os.path.exists(f):
        warn('''expected file "{}" not found (consider passing the project_root arg when setting up JS tests).
    Tilt can still install your JS deps, it just can't watch this file and reinstall JS deps on change.'''.format(f))
        return False
    return True


def deps_for_package_manager(pkmanager, project_root):
    install_deps = [os.path.join(project_root, 'package.json')]
    if pkmanager == 'npm':
        install_deps.append(os.path.join(project_root, 'package-lock.json'))
    elif pkmanager == 'yarn':
        install_deps.append(os.path.join(project_root, 'yarn.lock'))
    else:
        fail('unrecognized js package manager: {}'.format(pkmanager))

    return [dep for dep in install_deps if verify_dep(dep)]


def extra_ignores(pkmanager, project_root):
    files = ['.eslintcache']
    if pkmanager == 'yarn':
        files.append('yarn-error.log')
    return [os.path.join(project_root, f) for f in files]
