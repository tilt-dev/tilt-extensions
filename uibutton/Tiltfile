def cmd_button(name, resource, argv, text=''):
  if config.tilt_subcommand == 'down':
    return

  if not text:
    text = name

  button = {
    "apiVersion": "tilt.dev/v1alpha1",
    "kind": "UIButton",
    "metadata": {
      "name": name,
      "annotations": {
        "tilt.dev/resource": resource,
      }
    },
    "spec": {
      "text": text,
      "location": {
        "componentType": "Resource",
        "componentID": resource,
      }
    }
  }
  cmd = {
    "apiVersion": "tilt.dev/v1alpha1",
    "kind": "Cmd",
    "metadata": {
      "name": "btn-" + name,
      "annotations": {
        "tilt.dev/resource": resource,
        "tilt.dev/log-span-id": 'cmd:' + name,
      }
    },
    "spec": {
      "args": argv,
      "dir": config.main_dir,
      "startOn": {
        "startAfter": _now(),
        "uiButtons": [name],
      },
    }
  }
  local("echo %s | %s apply -f -" % (shlex.quote(str(encode_yaml_stream([button, cmd]))), sys.executable))


def _now():
  # this is portable across coreutils/busybox/BSD
  # note: it's missing fractional seconds because that's not available from strftime
  return str(local('date -u +"%Y-%m-%dT%H:%M:%S.000000Z"')).rstrip('\r\n')
