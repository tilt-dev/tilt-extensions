# -*- mode: Python -*-

def pack(
  name,
  path=".",
  builder="gcr.io/paketo-buildpacks/builder:base",
  buildpacks=[],
  env_vars=[],
  pull_policy="if-not-present",
  deps=[],
  **kwargs
  ):
    """
    Build a container image using pack and buildpacks.
    Args:
      name: name of the image to build.
      path: path to application directory, defaults to current working directory.
      builder: builder image, defaults to gcr.io/paketo-buildpacks/builder:base
      buildpacks: A list of buildpacks to use. (list[str])
      env_vars: A list of environment variables. (list[str])
      **kwargs: will be passed to the underlying `custom_build` call
    """

    # Remove possible tag from image name
    name = name.split(":")[0]

    envs = list(env_vars)
    if not bool([e for e in envs if ('BP_LIVE_RELOAD_ENABLED' in e)]):
        envs.append('BP_LIVE_RELOAD_ENABLED=true')

    deps_copy = list(deps)
    if len(deps_copy) == 0:
        deps_copy = [path]

    caching_ref = name + ":tilt-build-pack-caching"

    pack_build_cmd = " ".join([
      "pack build",
      caching_ref,
      "--path " + path,
      "--builder " + builder,
      "--pull-policy " + pull_policy,
      " ".join(["--buildpack " + s for s in buildpacks]),
      " ".join(["--env " + s for s in envs]),
    ])

    isWindows = True if os.name == "nt" else False
    expected_ref = "%EXPECTED_REF%" if isWindows else "$EXPECTED_REF"
    docker_tag_cmd = " ".join([
      "docker tag",
      caching_ref,
      expected_ref,
    ])

    custom_build(
        name,
        pack_build_cmd + " && " + docker_tag_cmd,
        deps=deps_copy,
        **kwargs
    )
