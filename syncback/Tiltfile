# -*- mode: Python -*-

krsync_path = os.path.join(os.getcwd(), 'krsync.sh')

MIN_LOCAL_RSYNC_VERSION = '3.0.0'
verify_rsync_path = os.path.join(os.getcwd(), 'verify_rsync.sh')

# if no local rsync/insufficient version, bail
print('-- syncback extension checking for local rsync --')
local('{} {}'.format(verify_rsync_path, MIN_LOCAL_RSYNC_VERSION))

def syncback(name, k8s_object, src_dir, paths=None, target_dir='.', container='', namespace='', verbose=False):
    """
    Create a local resource that will (via rsync) sync the specified files
    from the specified k8s object to the local filesystem.

    :param name (str): name of the created local resource.
    :param k8s_object (str): a Kubernetes object identifier (e.g. deploy/my-deploy, job/my-job, or a pod ID) that Tilt
           can use to select a pod. As per the behavior of `kubectl exec`, we will act on the first pod of the specified
           object, using the first container by default.
    :param src_dir (str): directory IN THE KUBERNETES CONTAINER to sync from. Any paths specified, if relative,
           should be relative to this dir.
    :param paths (List[str], optional): paths IN THE KUBERNETES CONTAINER to sync, relative to src_dir. May be files or dirs.
           Note that these must not begin with `./`. If no paths are provided, sync the entire src_dir.
    :param target_dir (str, optional): directory ON THE LOCAL FS to sync to. Defaults to '.'
    :param container (str, optiona): name of the container to sync from (by default, the first container)
    :param namespace (str, optiona): namespace of the desired k8s_object, if not `default`.
    :param verbose (bool, optional): if true, print additional rsync information.
    :return:
    """

    incl_excl = ''
    if paths:
        # TODO: if you're rsync-savvy you might want to do the wildcarding manually--
        #   give an option to turn off automatic +'***'
        includes = ' '.join(['--include="{}***"'.format(p) for p in paths])
        incl_excl = '{} --exclude="*"'.format(includes)

    if not src_dir.endswith('/'):
        fail('src_dir must be a directory and have a trailing slash')

    remote_name = 'dummy'
    if namespace:
        remote_name = 'dummy@{}'.format(namespace)

    # instead of wrestling with passing optional args to krsync.sh that do not then
    # get passed to rsync, just bundle container flag with k8s object specifier
    if container:
        k8s_object = '"{obj} -c {container}"'.format(obj=k8s_object, container=container)

    flags = '-aOv'
    if verbose:
        flags = '-aOvvi'

    local_resource(name, '{krsync} {obj} {flags} --progress --stats --delete -T=/tmp/rsync.tilt {include_exclude} {remote}:{src} {target}'.
                   format(krsync=krsync_path, obj=k8s_object, flags=flags, include_exclude=incl_excl,
                          remote=remote_name, src=src_dir, target=target_dir),
               trigger_mode=TRIGGER_MODE_MANUAL, auto_init=False)

    # TODO: not necessarily manual/can link up to a resource as a dep

