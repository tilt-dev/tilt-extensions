# -*- mode: Python -*-

krsync_path = os.path.join(os.getcwd(), 'krsync.sh')

MIN_LOCAL_RSYNC_VERSION = '3.0.0'
verify_rsync_path = os.path.join(os.getcwd(), 'verify_rsync.sh')

# if no local rsync/insufficient version, bail
print('-- syncback extension checking for local rsync --')
local('{} {}'.format(verify_rsync_path, MIN_LOCAL_RSYNC_VERSION))

DEFAULT_EXCLUDES = ['.git', '.gitignore', '.dockerignore', 'Dockerfile', '.tiltignore', 'Tiltfile', 'tilt_modules']

def syncback(name, k8s_object, src_dir, ignore=None, delete=False, paths=None, target_dir='.', container='', namespace='', verbose=False):
    """
    Create a local resource that will (via rsync) sync the specified files
    from the specified k8s object to the local filesystem.

    :param name (str): name of the created local resource.
    :param k8s_object (str): a Kubernetes object identifier (e.g. deploy/my-deploy, job/my-job, or a pod ID) that Tilt
           can use to select a pod. As per the behavior of `kubectl exec`, we will act on the first pod of the specified
           object, using the first container by default.
    :param src_dir (str): directory IN THE KUBERNETES CONTAINER to sync from. Any paths specified, if relative,
           should be relative to this dir.
    :param ignore (List[str], optional): files to ignore when syncing (relative to src_dir).
    :param delete (bool, optional): run rsync with the --delete flag, i.e. delete files locally if not present in
           the container. By default, False. THIS OPTION RISKS WIPING OUT FILES that exist locally but not in the
           container. Tilt will protect some files automatically, but we recommend syncing specific paths (via `paths`
           and/or using the `ignore` parameter to explicitly protect other files that exist locally but not on the container.
    :param paths (List[str], optional): paths IN THE KUBERNETES CONTAINER to sync, relative to src_dir. May be files or dirs.
           Note that these must not begin with `./`. If this arg is not passed, sync all of src_dir.
    :param target_dir (str, optional): directory ON THE LOCAL FS to sync to. Defaults to '.'
    :param container (str, optiona): name of the container to sync from (by default, the first container)
    :param namespace (str, optiona): namespace of the desired k8s_object, if not `default`.
    :param verbose (bool, optional): if true, print additional rsync information.
    :return:
    """

    # Verify inputs
    if not src_dir.endswith('/'):
        fail('src_dir must be a directory and have a trailing slash (because of rsync syntax rules)')

    if paths:
        for p in paths:
            if p.startswith('./'):
                fail('Found illegal path "{}": paths may not begin with ./ (because of rsync syntax rules)'.format(p))
            if p.startswith('/'):
                fail('Found illegal path "{}": paths may not begin with / and must be relative to src_dir (because of rsync syntax rules)'.format(p))

    # Construct include/exclude rules
    incl_excl = ''

    to_exclude = ignore
    if not ignore:
        to_exclude = []

    if paths:
        # TODO: if you're rsync-savvy you might want to do the wildcarding manually--
        #   give an option to turn off automatic +'***'
        includes = ' '.join(['--include="{}***"'.format(p) for p in paths])
        incl_excl = '{} --exclude="*"'.format(includes)
    else:
        # Sync the entire src_dir. Danger, Will Robinson! Exclude some stuff
        # that may exist locally but not in your container so it
        # doesn't get wiped out locally on your first sync
        to_exclude = DEFAULT_EXCLUDES + to_exclude

    excludes = ' '.join(['--exclude="{}***"'.format(ex) for ex in to_exclude])
    incl_excl = '{} {}'.format(excludes, incl_excl)

    # construct remote name (maybe with namespace)
    # (when we pass `dummy@namespace` to krsync, it gets parsed and then passed to
    # `rsync --rsh` as two args, `dummy` and `namespace`. We grab the latter to use,
    # and ignore the former in favor of k8s_object info we stashed in an env variable
    remote_name = 'dummy'
    if namespace:
        remote_name = 'dummy@{}'.format(namespace)

    # instead of wrestling with passing optional args to krsync.sh that do not then
    # get passed to rsync, just bundle container flag with k8s object specifier
    if container:
        k8s_object = '"{obj} -c {container}"'.format(obj=k8s_object, container=container)

    flags = '-aOv'
    if verbose:
        flags = '-aOvvi'

    delete_flag = ''
    if delete:
        delete_flag = '--delete'
    local_resource(name, '{krsync} {obj} {flags} --progress --stats {delete} -T=/tmp/rsync.tilt {include_exclude} {remote}:{src} {target}'.
                   format(krsync=krsync_path, obj=k8s_object, flags=flags, delete=delete_flag,
                          include_exclude=incl_excl, remote=remote_name, src=src_dir, target=target_dir),
               trigger_mode=TRIGGER_MODE_MANUAL, auto_init=False)

    # TODO: not necessarily manual/can link up to a resource as a dep

