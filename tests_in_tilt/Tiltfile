# Some common test configurations to easily get up and running with Tilt for Tests
# (see docs: https://docs.tilt.dev/tests_in_tilt.html)

### GOLANG TESTS ###
def test_go_all(path, deps, timeout='', tags=None, mod=''):
    # path: str, deps: List[str], timeout: str='', tags: List[str]=None, mod: str=''
    if not path.endswith('...'):
        path = path.rstrip('/')
        path = path + '/...'

    timeout_str = ''
    if timeout:  # expects a go-parsable dur
        timeout_str = '-timeout {}'.format(timeout)
    tags_str = ''
    if tags:
        tags_str = '-tags {}'.format(','.join(tags))

    mod_str = ''
    if mod:
        mod_str = '-mod {}'.format(mod)

    cmd = 'go test {mod_str} {tags_str} {timeout_str} {path}'.format(
        mod_str=mod_str, tags_str=tags_str, timeout_str=timeout_str, path=path)
    test('test-go-all', cmd, deps=deps)


### JAVASCRIPT TESTS ###
def _test_js_all(pkmanager, dir, deps=None, with_install=False):
    resource_deps = []
    if with_install:
        name = pkmanager + '-install'
        local_resource(name, 'cd {dir} && {pkmanager} install'.format(
            dir=dir, pkmanager=pkmanager))
        resource_deps = [name]

    cmd = 'cd {dir} && {pkmanager} test -o --watchAll=false'.format(
        dir=dir, pkmanager=pkmanager)

    file_deps = deps
    if not file_deps:
        file_deps = dir

    test('test-js-all', cmd, deps=file_deps, resource_deps=resource_deps)


def test_jest_npm_all(dir, deps=None, with_install=False):
    _test_js_all('npm', dir, deps, with_install)


def test_jest_yarn_all(dir, deps=None, with_install=False):
    _test_js_all('yarn', dir, deps, with_install)
